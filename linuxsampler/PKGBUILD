# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=linuxsampler
pkgver=2.1.1
pkgrel=2
pkgdesc="Professional-grade audio sampler alternative to Gigasampler"
arch=('x86_64' 'armv7h')
url="https://www.linuxsampler.org/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libgig.so' 'libjack.so' 'sqlite')
makedepends=('dssi' 'ladspa' 'lv2')
provides=('liblinuxsampler.so')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.bz2" "RTMath.cpp.patch" "atomic.h.patch")
sha512sums=('3f172efe40bbe130a2ac09f9393a46a30cc327a6754a80fdb36e65276518ec82befa2075acc567ccd99c22d9859c1a65f58ff2ad7af70cd9e4649ae14fd5357c'
            '425a195bb94af9728dfbdb0967dcc0989df069982fba980b60c4e8c82293c5a471ae73cb591063a52d0144cb987f6b5b3a018e862326c8c8d7b7f21f50e33dd6'
            '53d137cb830c1f3191a3f983bb0cd0131ae6ea0f6c2dd8488b449ffbe175e16b48f1c961045d369b2267875a332b541178f219e6150d1c00b358c3e2682943d0')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/RTMath.cpp.patch"
  patch --forward --strip=1 --input="${srcdir}/atomic.h.patch"
  autoreconf -vfi
  # creating a ld.so.conf entry for linuxsampler.so
  echo "/usr/lib/${pkgname}" > "${pkgname}.conf"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # adding linuxsampler.so library path to ld.so.conf (required for dependants
  # during runtime)
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/ld.so.conf.d/"
  # lscp files conflict with nilfs-utils:
  # https://bugs.archlinux.org/task/45827
  mv -v "${pkgdir}/usr/bin/lscp" "${pkgdir}/usr/bin/lscp-${pkgname}"
  mv -v "$pkgdir/usr/share/man/man1/lscp.1" \
    "${pkgdir}/usr/share/man/man1/lscp-${pkgname}.1"
}
# vim:set ts=2 sw=2 et:
